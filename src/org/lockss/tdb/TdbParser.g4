/*

Copyright (c) 2000-2022, Board of Trustees of Leland Stanford Jr. University
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation
and/or other materials provided with the distribution.

3. Neither the name of the copyright holder nor the names of its contributors
may be used to endorse or promote products derived from this software without
specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
POSSIBILITY OF SUCH DAMAGE.

*/

parser grammar TdbParser;

@parser::header {

/* ===================================================================

WARNING - WARNING - WARNING - WARNING - WARNING - WARNING - WARNING

If you are modifying a Java file with this warning in it, you are
making changes in the wrong place. This Java file was generated by
ANTLR from a grammar file. You should be editing the grammar file
instead.

=================================================================== */

}

options { tokenVocab = TdbLexer; }

tdb                     : publisherContainer* EOF ;

publisherContainer      : CURLY_OPEN assignment* publisherContainer* CURLY_CLOSE
                        | CURLY_OPEN publisher assignment* titleContainer* CURLY_CLOSE
                        ;

publisher               : PUBLISHER ANGLE_OPEN listOfSimpleAssignments ANGLE_CLOSE ;

titleContainer          : CURLY_OPEN assignment* titleContainer* CURLY_CLOSE
                        | CURLY_OPEN title assignment* auContainer* CURLY_CLOSE
                        ;

title                   : TITLE ANGLE_OPEN listOfSimpleAssignments ANGLE_CLOSE ;

auContainer             : CURLY_OPEN assignment* auContainer* CURLY_CLOSE
                        | au
                        ;

au                      : AU ANGLE_OPEN listOfStrings ANGLE_CLOSE ;

assignment              : simpleAssignment
                        | implicit
                        ;

simpleAssignment        : IDENTIFIER EQUALS STRING ;

implicit                : IMPLICIT ANGLE_OPEN listOfIdentifiers ANGLE_CLOSE ;

listOfSimpleAssignments : simpleAssignment ( SEMICOLON simpleAssignment )* ;

listOfStrings           : STRING ( SEMICOLON STRING )* ;

listOfIdentifiers       : IDENTIFIER ( SEMICOLON IDENTIFIER )* ;
